\ This file passes if it runs without throwing any errors.
\ The Rust test that runs this file is in lib.rs.

\ Syntax. Note that these aren't actually evaluated at runtime.
{
  1
  _2
  0.5
  "a"
  ["a"]
  c0
  C0

  1 (2 3) c0 _+ 4 
  [1; 2 3; c0; _+; 4]
  [1
   2 3; c0
   _+; 4]

  {[a] 1}  \ comment
  {[_] 1}

  {: [a] 1}
  { :[a] 1}
  {:[a] 1 :[b] 1}
  { : [a] 1 : [b] 1}

  F: +
  f[_; 3]
  f[; 3]
  f[3; _]
  f[3; ]
  f[_; _]
  f[;]

  \ Past bugs
  \ https://github.com/stackofbears/arilus/commit/80727dcc76703f7811abe29fcb8ad2efb385c3ef
  "babcab" = "b" ->eq \:+ ->acc  
}

\ Evaluation
1 + 1 -> 2
2 - 3 -> _1
"abc" + 2 -> "cde"
c0 + 65 -> "A"
1 2 3 '{x + 3 == y} 4 5 6 -> 1 1 1
1 2 3 `In 3 2 1 -> 1 1 1
1 2 3 4 \:+ -> 1 3 6 10
1 2 3 4 \:+ 1 -> 2 4 7 11

"a" == ["a"] -> 0
"ab" @ 0 == "a" -> 1
"ab" @ 0 {[["a"]] 1 :["a"] 2} -> 2

1 2 3 4 (@ 1 2)<-(+ 10) -> 1 12 13 4

Binary: {
  r: if(x=0; [0]
       x { if(x=0; y; x // 2 Rec (x % 2 , y)) } [])
  r Yield[{ x * (2 ^ (x#/) |) \+ }]
}
11 Binary -> 1 0 1 1
11 Binary<-($ 1) -> 3
11 Binary<-(, 0) -> 22

Fahrenheit: {[c] c * 1.8 + 32 Yield[{x - 32 / 1.8}] }
5 Fahrenheit -> 41
5 Fahrenheit<-(+ 9) -> 10

FirstWord: { x = " " ? 1 -> i ~# x Yield[, (x $ i)] }
"Hi there" FirstWord<-{x , x[_1 _1]} -> "Hiii there"

\ Past bugs
\ https://github.com/stackofbears/arilus/commit/36bae6db7032d2ced97aeda738ab6fe3da508450
2 - (a: 1) -> 1
\ https://github.com/stackofbears/arilus/commit/eecf4f227bba23b7981d45d3ab4835ee8beae065
1 2 ?: 1 2 3
